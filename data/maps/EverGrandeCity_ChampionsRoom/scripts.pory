raw `
.set LOCALID_WALLACE, 1
.set LOCALID_RIVAL, 2
.set LOCALID_BIRCH, 3

EverGrandeCity_ChampionsRoom_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, EverGrandeCity_ChampionsRoom_OnTransition
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, EverGrandeCity_ChampionsRoom_OnWarp
	map_script MAP_SCRIPT_ON_FRAME_TABLE, EverGrandeCity_ChampionsRoom_OnFrame
	.byte 0

EverGrandeCity_ChampionsRoom_OnTransition:
	call Common_EventScript_SetupRivalGfxId
	end

EverGrandeCity_ChampionsRoom_OnWarp:
	map_script_2 VAR_TEMP_1, 0, EverGrandeCity_ChampionsRoom_EventScript_PlayerTurnNorth
	.2byte 0

EverGrandeCity_ChampionsRoom_EventScript_PlayerTurnNorth::
	turnobject OBJ_EVENT_ID_PLAYER, DIR_NORTH
	end

EverGrandeCity_ChampionsRoom_OnFrame:
	map_script_2 VAR_TEMP_1, 0, EverGrandeCity_ChampionsRoom_EventScript_EnterRoom
	.2byte 0

`

script EverGrandeCity_ChampionsRoom_EventScript_EnterRoom{
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp4)
	waitmovement(0)
	delay(40)
	applymovement(OBJ_EVENT_ID_PLAYER, EverGrandeCity_ChampionsRoom_Movement_PlayerApproachWallace)
	waitmovement(0)
	setvar(VAR_TEMP_1, 1)
	goto(EverGrandeCity_ChampionsRoom_EventScript_Wallace)
	releaseall
	end
}

movement EverGrandeCity_ChampionsRoom_Movement_PlayerApproachWallace{
	walk_up
	walk_up
	step_end
}

script EverGrandeCity_ChampionsRoom_EventScript_Wallace{
	playbgm(MUS_ENCOUNTER_CHAMPION, FALSE)
	msgbox(EverGrandeCity_ChampionsRoom_Text_IntroSpeech, MSGBOX_DEFAULT)
	trainerbattle_no_intro(TRAINER_WALLACE, EverGrandeCity_ChampionsRoom_Text_Defeat)
	goto(EverGrandeCity_ChampionsRoom_EventScript_Defeated)
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_Defeated{
	playse(SE_DOOR)
	setmetatile(6, 1, METATILE_EliteFour_OpenDoorChampion_Frame, FALSE)
	setmetatile(6, 2, METATILE_EliteFour_OpenDoorChampion_Opening, FALSE)
	special(DrawWholeMapView)
	msgbox(EverGrandeCity_ChampionsRoom_Text_PostBattleSpeech, MSGBOX_DEFAULT)
	closemessage
	playse(SE_DOOR)
	checkplayergender
	if(var(VAR_RESULT)==MALE){
		call(EverGrandeCity_ChampionsRoom_EventScript_PlayMayMusic)
		}
	else{
		call(EverGrandeCity_ChampionsRoom_EventScript_PlayBrendanMusic)
		}
	addobject(LOCALID_RIVAL)
	call(EverGrandeCity_ChampionsRoom_EventScript_RivalApproachPlayer)
	checkplayergender
		if(var(VAR_RESULT)==MALE){
		goto(EverGrandeCity_ChampionsRoom_EventScript_MayAdvice)
		}
	else{
		goto(EverGrandeCity_ChampionsRoom_EventScript_BrendanAdvice)
		}
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_PlayMayMusic{
	playbgm(MUS_ENCOUNTER_MAY, FALSE)
	return
}

script EverGrandeCity_ChampionsRoom_EventScript_PlayBrendanMusic{
	playbgm(MUS_ENCOUNTER_BRENDAN, FALSE)
	return
}

script EverGrandeCity_ChampionsRoom_EventScript_MayAdvice{
	msgbox(EverGrandeCity_ChampionsRoom_Text_MayAdvice, MSGBOX_DEFAULT)
	delay(40)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
	waitmovement(0)
	call(EverGrandeCity_ChampionsRoom_EventScript_RivalLookBackAndForth)
	msgbox(EverGrandeCity_ChampionsRoom_Text_MayItsAlreadyOver, MSGBOX_DEFAULT)
	goto(EverGrandeCity_ChampionsRoom_EventScript_BirchArrivesExitForHoF)
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_BrendanAdvice{
	msgbox(EverGrandeCity_ChampionsRoom_Text_BrendanAdvice, MSGBOX_DEFAULT)
	delay(40)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
	waitmovement(0)
	call(EverGrandeCity_ChampionsRoom_EventScript_RivalLookBackAndForth)
	msgbox(EverGrandeCity_ChampionsRoom_Text_BrendanYouveWon, MSGBOX_DEFAULT)
	goto(EverGrandeCity_ChampionsRoom_EventScript_BirchArrivesExitForHoF)
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_BirchArrivesExitForHoF{
	closemessage
	addobject(LOCALID_BIRCH)
	applymovement(LOCALID_BIRCH, EverGrandeCity_ChampionsRoom_Movement_BirchArrives)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	msgbox(EverGrandeCity_ChampionsRoom_Text_BirchArriveRatePokedex, MSGBOX_DEFAULT)
	msgbox(EverGrandeCity_ChampionsRoom_Text_BirchCongratulations, MSGBOX_DEFAULT)
	applymovement(LOCALID_WALLACE, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)
	delay(20)
	applymovement(LOCALID_WALLACE, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	msgbox(EverGrandeCity_ChampionsRoom_Text_WallaceComeWithMe, MSGBOX_DEFAULT)
	closemessage
	delay(30)
	applymovement(LOCALID_WALLACE, EverGrandeCity_ChampionsRoom_Movement_WallaceExitStart)
	applymovement(OBJ_EVENT_ID_PLAYER, EverGrandeCity_ChampionsRoom_Movement_PlayerExitStart)
	applymovement(LOCALID_BIRCH, Common_Movement_WalkInPlaceFasterUp)
	applymovement(LOCALID_RIVAL, EverGrandeCity_ChampionsRoom_Movement_RivalFollows)
	waitmovement(0)
	delay(20)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	msgbox(EverGrandeCity_ChampionsRoom_Text_WallaceWaitOutside, MSGBOX_DEFAULT)
	checkplayergender
			if(var(VAR_RESULT)==MALE){
		call(EverGrandeCity_ChampionsRoom_EventScript_MayCongratulations)
		}
	else{
		call(EverGrandeCity_ChampionsRoom_EventScript_BrendanCongratulations)
		}
	closemessage
	applymovement(LOCALID_WALLACE, EverGrandeCity_ChampionsRoom_Movement_WallaceExit)
	applymovement(OBJ_EVENT_ID_PLAYER, EverGrandeCity_ChampionsRoom_Movement_PlayerExit)
	waitmovement(0)
	setflag(FLAG_HIDE_PETALBURG_GYM_GREETER)
	warp(MAP_EVER_GRANDE_CITY_HALL_OF_FAME, 7, 16)
	waitstate
	releaseall
	end
}

script EverGrandeCity_ChampionsRoom_EventScript_MayCongratulations{
	msgbox(EverGrandeCity_ChampionsRoom_Text_MayCongratulations, MSGBOX_DEFAULT)
	return
}

script EverGrandeCity_ChampionsRoom_EventScript_BrendanCongratulations{
	msgbox(EverGrandeCity_ChampionsRoom_Text_BrendanCongratulations, MSGBOX_DEFAULT)
	return
}

script EverGrandeCity_ChampionsRoom_EventScript_RivalApproachPlayer{
	applymovement(LOCALID_RIVAL, EverGrandeCity_ChampionsRoom_Movement_RivalApproachPlayer)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement(0)
	return
}

script EverGrandeCity_ChampionsRoom_EventScript_RivalLookBackAndForth{
	applymovement(LOCALID_RIVAL, EverGrandeCity_ChampionsRoom_Movement_RivalLookBackAndForth)
	waitmovement(0)
	return
}

movement EverGrandeCity_ChampionsRoom_Movement_PlayerExitStart{
	walk_up
	walk_up
	step_end
}

movement EverGrandeCity_ChampionsRoom_Movement_PlayerExit{
	walk_up
	delay_16
	walk_up
	delay_8
	set_invisible
	step_end
}

movement EverGrandeCity_ChampionsRoom_Movement_RivalApproachPlayer{
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_left
	walk_up
	walk_in_place_faster_right
	step_end
}

movement EverGrandeCity_ChampionsRoom_Movement_RivalLookBackAndForth{
	walk_in_place_faster_up
	delay_16
	walk_in_place_faster_right
	delay_16
	walk_in_place_faster_up
	delay_8
	walk_in_place_faster_right
	delay_16
	step_end
}

movement EverGrandeCity_ChampionsRoom_Movement_RivalFollows{
	walk_right
	walk_up
	step_end
}

movement EverGrandeCity_ChampionsRoom_Movement_WallaceExitStart{
	walk_up
	walk_up
	walk_in_place_faster_down
	step_end
}

movement EverGrandeCity_ChampionsRoom_Movement_WallaceExit{
	walk_up
	delay_8
	set_invisible
	step_end
}

movement EverGrandeCity_ChampionsRoom_Movement_BirchArrives{
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_right
	walk_up
	walk_in_place_faster_left
	step_end
}

text EverGrandeCity_ChampionsRoom_Text_IntroSpeech{
	"ABIGAIL: Oh hi angel!\p"
	"You've made it this far,\n"
	"that's so cool!\p"
	"I guess we'll be seeing you\n"
	"at the show later.\p"
	"I'm Abigail by the way, I'll be the\n"
	"songbird screaming in your face.\p"
	"Sorry, maybe I should have started\n"
	"with that.\p"
	"Anyway, I'm sure you slay but you\n"
	"know, I have to make sure that you're\l"
	"not just here for entertainment.\p"
	"So pull your boots up,\n"
	"and commit to the role,\l"
	"fight for that ticket!$"
}

text EverGrandeCity_ChampionsRoom_Text_Defeat{
	"I can't win them all…\p"
	"…\p"
	"Period! You've just earned\n"
	"yourself a Last Dinner Party\l"
	"ticket, {PLAYER}{KUN}!$"
}

text EverGrandeCity_ChampionsRoom_Text_PostBattleSpeech{
	"ABIGAIL: Wow that was amazing!\p"
	"I had a feeling you'd be a fucking\n"
	"icon.\p"
	"You've totally earned your spot at\n"
	"the show, I can't wait to see you\l"
	"on the front row!\p"
	"Now all we need to do is add you\n"
	"to the…$"
}

text EverGrandeCity_ChampionsRoom_Text_MayAdvice{
	"GOBLIN: {PLAYER}{KUN}!\p"
	"Here's some advice before you challenge\n"
	"Abi…$"
}

text EverGrandeCity_ChampionsRoom_Text_MayItsAlreadyOver{
	"GOBLIN: Huh?\n"
	"What, what, what?\p"
	"{PLAYER}{KUN}…\n"
	"Could it be that…\l"
	"It's already over?$"
}

text EverGrandeCity_ChampionsRoom_Text_BrendanAdvice{
	"GOBLIN: {PLAYER}!\p"
	"How would you like some advice before\n"
	"you challenge Abi?…$"
}

text EverGrandeCity_ChampionsRoom_Text_BrendanYouveWon{
	"GOBLIN: {PLAYER}…\p"
	"Are you trying to tell me…\n"
	"You've won already?$"
}

text EverGrandeCity_ChampionsRoom_Text_BirchArriveRatePokedex{
	"AMY: See?\n"
	"What did I tell you, Goblin?\p"
	"Didn't I tell you that you don't need\n"
	"to worry about {PLAYER}{KUN}?\p"
	"… … … … … … … …\n"
	"{PLAYER}{KUN}, you've really done it!\p"
	"You're seeing The Last Dinner Party!$"
}

text EverGrandeCity_ChampionsRoom_Text_BirchCongratulations{
	"AMY: Anyways,\n"
	"congratulations!\p"
	"Now, go proudly into the final room!$"
}

text EverGrandeCity_ChampionsRoom_Text_WallaceComeWithMe{
	"ABIGAIL: {PLAYER}{KUN}…\p"
	"No, let me rephrase that properly.\p"
	"Our new SPECIAL DINNER GUEST!\p"
	"Come with me.$"
}

text EverGrandeCity_ChampionsRoom_Text_WallaceWaitOutside{
	"ABIGAIL: I'm sorry, but…\p"
	"From here on, only those who have\n"
	"won the challenge may enter.\p"
	"You'll have to wait outside with\n"
	"Amy.$"
}

text EverGrandeCity_ChampionsRoom_Text_MayCongratulations{
	"GOBLIN: Awh…\p"
	"… … … … … … … …\n"
	"… … … … … … … …\p"
	"Maybe I should have tried to win\n"
	"my ticket that way, then…\p"
	"I'm just joking! That's okay!\n"
	"That's the rule!\p"
	"{PLAYER}{KUN}!\n"
	"Honestly, congratulations!\p"
	"See you at the barricade!$"
}

text EverGrandeCity_ChampionsRoom_Text_BrendanCongratulations{
	"GOBLIN: Whaaaat?! \p"
	"… … … … …\n"
	"… … … … … … … …\p"
	"But I was dying to know what\n"
	"was in there…\p"
	"It's okay, that's the rule.\p"
	"{PLAYER}, way to go!\n"
	"Congratulations! See you at\n"
	"the barricade!$"
}